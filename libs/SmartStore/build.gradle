buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

dependencies {
  compile project(':libs:SalesforceSDK')
  compile 'net.zetetic:android-database-sqlcipher:3.5.2'
}

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.0'
  
  buildTypes {
      debug {
         testCoverageEnabled = true
      }
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      resources.srcDirs = ['src']
      aidl.srcDirs = ['src']
      renderscript.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
      jniLibs.srcDir 'libs'
    }
    androidTest.setRoot('../test/SmartStoreTest')
    androidTest {
      java.srcDirs = ['../test/SmartStoreTest/src']
      resources.srcDirs = ['../test/SmartStoreTest/src']
      res.srcDirs = ['../test/SmartStoreTest/res']
    }
  }
  defaultConfig {
    testApplicationId "com.salesforce.androidsdk.smartstore.tests"
    testInstrumentationRunner "com.salesforce.androidsdk.analytics.util.test.JUnitReportTestRunner"
  }
  packagingOptions {
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
  }

  lintOptions {
    abortOnError false
  }
}

afterEvaluate {
  // Find all connected Android tests and ensure they log all passed tests. This keeps the Travis
  // from timing out since executing tests are printed to the console.
  tasks.matching { it.name ==~ /^connected.*AndroidTest$/ }*.logging*.setLevel(LogLevel.INFO)
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'SmartStore'
                url 'https://github.com/forcedotcom/SalesforceMobileSDK-Android'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'bhariharan'
                        name 'Bharath Hariharan'
                        email 'bhariharan@salesforce.com'
                    }
                }
                scm {
                    connection 'https://github.com/forcedotcom/SalesforceMobileSDK-Android.git'
                    developerConnection 'https://github.com/forcedotcom/SalesforceMobileSDK-Android.git'
                    url 'https://github.com/forcedotcom/SalesforceMobileSDK-Android'

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'salesforcemobilesdk'
        name = 'smart-store'
        userOrg = 'forcedotcom'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/forcedotcom/SalesforceMobileSDK-Android.git'
        websiteUrl = 'https://github.com/forcedotcom/SalesforceMobileSDK-Android'
        issueTrackerUrl = 'https://github.com/forcedotcom/SalesforceMobileSDK-Android/issues'
        publicDownloadNumbers = true
        githubRepo = 'forcedotcom/SalesforceMobileSDK-Android'
        githubReleaseNotesFile = 'README.md'
        licenses = ['Apache-2.0']
        labels = ['android', 'salesforce', 'mobilesdk']
        version {
            name = '5.2.0'
            released  = new Date()
            vcsTag = 'v5.2.0'
        }
    }
}
