buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

dependencies {
    api project(':libs:SmartStore')
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}

android {
    compileSdkVersion 29

    defaultConfig {
        targetSdkVersion 29
        minSdkVersion 23
    }
  
  buildTypes {
      debug {
         testCoverageEnabled = true
      }
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      resources.srcDirs = ['src']
      aidl.srcDirs = ['src']
      renderscript.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }

    androidTest.setRoot('../test/MobileSyncTest')
    androidTest {
      java.srcDirs = ['../test/MobileSyncTest/src']
      resources.srcDirs = ['../test/MobileSyncTest/src']
      res.srcDirs = ['../test/MobileSyncTest/res']
    }
  }
  defaultConfig {
    testApplicationId "com.salesforce.androidsdk.mobilesync.tests"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  packagingOptions {
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
  }

  lintOptions {
    xmlReport true 
    abortOnError false
  }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'MobileSync'
                url 'https://github.com/forcedotcom/SalesforceMobileSDK-Android'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'bhariharan'
                        name 'Bharath Hariharan'
                        email 'bhariharan@salesforce.com'
                    }
                }
                scm {
                    connection 'https://github.com/forcedotcom/SalesforceMobileSDK-Android.git'
                    developerConnection 'https://github.com/forcedotcom/SalesforceMobileSDK-Android.git'
                    url 'https://github.com/forcedotcom/SalesforceMobileSDK-Android'

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'salesforcemobilesdk'
        name = 'mobile-sync'
        userOrg = 'forcedotcom'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/forcedotcom/SalesforceMobileSDK-Android.git'
        websiteUrl = 'https://github.com/forcedotcom/SalesforceMobileSDK-Android'
        issueTrackerUrl = 'https://github.com/forcedotcom/SalesforceMobileSDK-Android/issues'
        publicDownloadNumbers = true
        githubRepo = 'forcedotcom/SalesforceMobileSDK-Android'
        githubReleaseNotesFile = 'README.md'
        licenses = ['Apache-2.0']
        labels = ['android', 'salesforce', 'mobilesdk']
        version {
            name = '8.0.0'
            released  = new Date()
            vcsTag = 'v8.0.0'
        }
    }
}
