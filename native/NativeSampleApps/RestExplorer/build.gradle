apply plugin: 'com.android.application'

dependencies {
  api project(':libs:SalesforceSDK')
  androidTestImplementation ('com.android.support.test:runner:1.0.1') {
    exclude module: 'support-annotations'
  }
  androidTestImplementation ('com.android.support.test:rules:1.0.1') {
    exclude module: 'support-annotations'
  }
  androidTestImplementation ('com.android.support.test.espresso:espresso-core:3.0.1') {
    exclude module: 'support-annotations'
  }
}

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.3'
  
  buildTypes {
      debug {
         testCoverageEnabled = true
      }
   }

  sourceSets {

    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      resources.srcDirs = ['src']
      aidl.srcDirs = ['src']
      renderscript.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }

    androidTest.setRoot('../test/RestExplorerTest')
    androidTest {
      java.srcDirs = ['../test/RestExplorerTest/src']
      resources.srcDirs = ['../test/RestExplorerTest/src']
      res.srcDirs = ['../test/RestExplorerTest/res']
    }
  }
  defaultConfig {
    testApplicationId "com.salesforce.samples.restexplorer.tests"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  packagingOptions {
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
    pickFirst 'protobuf.meta'
  }

  lintOptions {
    xmlReport true
    abortOnError false
  }
}

afterEvaluate {
  // Find all connected Android tests and ensure they log all passed tests. This keeps the Travis
  // from timing out since executing tests are printed to the console.
  tasks.matching { it.name ==~ /^connected.*AndroidTest$/ }*.logging*.setLevel(LogLevel.INFO)
}
